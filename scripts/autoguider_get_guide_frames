#!/bin/csh
# $Header: /home/cjm/cvs/autoguider/scripts/autoguider_get_guide_frames,v 1.8 2013-12-09 10:25:44 cjm Exp $
set gaiadisp = 0
set ds9disp = 0
set ds9objects = 0
set delete_frames = 0
set frame_type_string = "raw"
set sleep_time = 2
set filename_dir = "/icc/tmp/"
if ( $#argv > 0 ) then
    set next_arg = ""
    foreach arg ( $argv )
	if( "$arg" == "-delete" ) then
		set delete_frames = 1
	else if( "$arg" == "-ds9disp" ) then
		set ds9disp = 1
	else if( "$arg" == "-ds9objects" ) then
		set ds9objects = 1
	else if( "$arg" == "-gaiadisp" ) then
		set gaiadisp = 1
	else if( "$arg" == "-raw" ) then
		set frame_type_string = "raw"
	else if( "$arg" == "-reduced" ) then
		set frame_type_string = "reduced"
	else if( "$arg" == "-sleep_time" ) then
		set next_arg = "sleep_time"
	else if( "$arg" == "-help" ) then
		echo "autoguider_get_guide_frames [-gaiadisp][-ds9disp [-ds9objects]][-raw|-reduced][-delete][-sleep_time <secs>]"
		exit 1
	else 
		if( "${next_arg}" == "sleep_time" ) then
		    set sleep_time = "${arg}"
		    set next_arg = ""
		else
		    echo "Unknown argument: $1"
		    exit 1
		endif
        endif
    end
endif
if ( -r /home/dev/bin/autoguider/commandserver/test/${HOSTTYPE}/test_getfits_command ) then
    alias getfits_command "/home/dev/bin/autoguider/commandserver/test/${HOSTTYPE}/test_getfits_command"
else if ( -r /icc/bin/autoguider/commandserver/test/${HOSTTYPE}/test_getfits_command ) then
    alias getfits_command "/icc/bin/autoguider/commandserver/test/${HOSTTYPE}/test_getfits_command"
else
    echo "Could not find test_getfits_command."
    exit 2
endif
if ( -r /home/dev/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command ) then
    alias send_command "/home/dev/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command"
else if ( -r /icc/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command ) then
    alias send_command "/icc/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command"
else
    echo "Could not find send_command."
    exit 2
endif
if ( -r /home/dev/bin/autoguider/scripts/print_line.awk ) then
    alias print_line "/home/dev/bin/autoguider/scripts/print_line.awk"
else if ( -r /icc/bin/autoguider/scripts/print_line.awk ) then
    alias print_line "/icc/bin/autoguider/scripts/print_line.awk"
else
    echo "Could not find print_line.awk."
    exit 3
endif
set hostname = "acc"
set port = "6571"
set file_number = 0
set done = 0
while ( "${done}" == 0 )
	set date_string = `/bin/date +"%Y-%m-%dT%H:%M:%S"`
	set filename = "${filename_dir}/guide_${frame_type_string}_${date_string}_${file_number}.fits"
	getfits_command -h ${hostname} -p ${port} -c "getfits guide ${frame_type_string}" -f ${filename}
	@ file_number ++
	if( "${gaiadisp}" == "1" ) then
		gaiadisp ${filename}
	endif
	#
	# ds9 display
	#
	if( "${ds9disp}" == "1" ) then
	    set ds9active = `xpaaccess ds9`
	    if( "${ds9active}" == "yes" ) then
		xpaset -p ds9 file ${filename}
	    else
		ds9 ${filename}&
		while( "${ds9active}" == "no" ) 
		    echo "Waiting for ds9 to become xpa active."
		    sleep 1
		    set ds9active = `xpaaccess ds9`
		end
	    endif
	endif
	#
	# display detected objects in ds9
	#
	if( "${ds9objects}" == "1" ) then
	    if( "${ds9disp}" == "0" ) then
		echo "-ds9objects set without -ds9disp being set:error."
		exit 1
	    endif
	    #
	    # get object count
	    #
	    set object_count_output = `send_command -h ${hostname} -p ${port} -c "status object count"`
	    echo "status object count returned : ${object_count_output}"
	    set object_count_output = `echo "${object_count_output}" | sed "s/.*client: reply:\(.*\)/\1/"`
	    echo "object count output now ${object_count_output}."
	    set retval = `echo "${object_count_output}" | /usr/bin/awk ' { print $1}'`
	    if( "${retval}" != "0" ) then
		echo "object count failed (${retval})."
		exit 1
	    endif
	    set object_count = `echo "${object_count_output}" | /usr/bin/awk ' { print $2}'`
	    if( "${object_count}" > "0" ) then
		#
		# get object list
		#
		set object_filename = "${filename_dir}/field_${frame_type_string}_${date_string}_${file_number}.txt"
		set object_list_output = `send_command -h ${hostname} -p ${port} -c "status object list" -f ${object_filename}`
		#
		# strip offheader lines
		#
		set line_count = `wc -l ${object_filename} | awk ' { print $1 }'`
		echo "object output has ${line_count} lines."
		@ line_count --
		@ line_count --
		set object_filename2 = "${filename_dir}/field_${frame_type_string}_${date_string}_${file_number}_headerless.txt"
		tail -n ${line_count} ${object_filename} > ${object_filename2}
		echo "Image annotations:"
		set line_number = 1
		while ( "${line_number}" <= "${line_count}" )
		    set line = `print_line -v LINE=${line_number} ${object_filename2}`
		    echo "Processing line: ${line}"
		    # 1. guide ID
		    # 2. Frame Number
		    # 3. Object index
		    # 4. CCD X
		    # 5. CCD Y
		    # 6. Buffer X
		    # 7. Buffer Y
		    # 8. Total Counts
		    # 9. Number of Pixels
		    # 10. Peak Counts
		    # 11. Is Steller (TRUE/FALSE)
		    # 12. FWHMX
		    # 13. FWHMY
		    set x = `echo ${line} | /usr/bin/awk ' { print $6}' | sed "s/\(.*\)\..*/\1/"`
		    set y = `echo ${line} | /usr/bin/awk ' { print $7}' | sed "s/\(.*\)\..*/\1/"`
		    echo "circle $x $y 10"
		    echo "circle $x $y 10" | xpaset ds9 regions
		    @ line_number ++
		end
            endif
	endif
	if( "${delete_frames}" == "1" ) then
	    /bin/rm ${filename}
	endif
	#
	# Are we still guiding?
	#
	#
	# get status guide active
	#
	set guide_active_output = `send_command -h ${hostname} -p ${port} -c "status guide active"`
	echo "guide active returned : ${guide_active_output}"
	set guide_active_output = `echo "${guide_active_output}" | sed "s/.*client: reply:\(.*\)/\1/"`
	echo "guide active output now ${guide_active_output}."
	set retval = `echo "${guide_active_output}" | /usr/bin/awk ' { print $1}'`
	if( "${retval}" != "0" ) then
	    echo "guide active failed (${retval})."
	    exit 1
	endif
	set guide_active = `echo "${guide_active_output}" | /usr/bin/awk ' { print $2}'`
	if( "${guide_active}" == "false" ) then
	    echo "guide active returned false:we have stopped autoguiding:stopping."
	    set done = 1
	endif
	if ( ${done} == 0 ) then
	    # sleep before getting next frame
	    sleep ${sleep_time}
	endif
end
