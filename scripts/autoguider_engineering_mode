#!/bin/csh
set machine_name = acc
set port = 6571
set ambient_temperature = 15.0
# Setup the ld library path
foreach dir ( /icc/bin/lib/${HOSTTYPE}/ )
    if ( ${?LD_LIBRARY_PATH} ) then
	echo ${LD_LIBRARY_PATH} | /bin/grep -q ":${dir}" > /dev/null
	if ( $status != 0 ) then
	    setenv LD_LIBRARY_PATH ${LD_LIBRARY_PATH}":"${dir}
	endif
    else
	setenv LD_LIBRARY_PATH ${dir}
    endif
end
/bin/echo "Setting autoguider temperature to: ${ambient_temperature}"
/icc/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command -h ${machine_name} -p ${port} -c "temperature set ${ambient_temperature}"
set done = 0
while ( ${done} == 0 )
	sleep 10
	set current_temp = `/icc/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command -h ${machine_name} -p ${port} -c "status temperature get" | awk ' { print $3}'`
	echo "Current autoguider temperature is ${current_temp}"
	set done = `/bin/echo " ( ${current_temp} > 0.0 )" | /usr/bin/bc`
end
#
# Kill any running processes
#
if ( -x /icc/bin/scripts/killhup.csh ) then
    echo "Trying to kill AutoBooter."
    /icc/bin/scripts/killhup.csh AutoBooter
else
    echo "killhup not found:FAILED to stop running processess."
endif
if ( -x /icc/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command ) then
    echo "Trying to kill autoguider by sending shutdown command."
    /icc/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command -h ${machine_name} -p 6571 -c "shutdown"
else
    echo "send_command not found:FAILED to stop running autoguider."
endif
