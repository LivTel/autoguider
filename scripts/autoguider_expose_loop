#!/bin/csh
# $Header: /home/cjm/cvs/autoguider/scripts/autoguider_field,v 1.2 2013-12-09 10:25:49 cjm Exp $
# Loop doing full frame autoguider exposures, and displaying in ds9 if possible
set ds9disp = 0
set exposure_length = 100
set frame_type_string = "reduced"
set filename_dir = "/icc/tmp/"
set delete_frames = 0
if ( $#argv > 0 ) then
    set next_arg = ""
    foreach arg ( $argv )
	if( "$arg" == "-delete_frames" ) then
		set delete_frames = 1
	else if( "$arg" == "-ds9disp" ) then
		set ds9disp = 1
	else if( "$arg" == "-exposure_length" ) then
		set next_arg = "exposure_length"
	else if( "$arg" == "-help" ) then
		echo "autoguider_expose_loop [-exposure_length <ms>][-help][-ds9disp][-delete_frames]"
		exit 1
	else 
		if( "${next_arg}" == "exposure_length" ) then
		    set exposure_length = "${arg}"
		    set next_arg = ""
		else
			echo "Unknown argument: $arg"
			exit 1
		endif
        endif
    end
endif
if ( -r /home/dev/bin/autoguider/commandserver/test/${HOSTTYPE}/test_getfits_command ) then
    alias getfits_command "/home/dev/bin/autoguider/commandserver/test/${HOSTTYPE}/test_getfits_command"
else if ( -r /icc/bin/autoguider/commandserver/test/${HOSTTYPE}/test_getfits_command ) then
    alias getfits_command "/icc/bin/autoguider/commandserver/test/${HOSTTYPE}/test_getfits_command"
else
    echo "Could not find test_getfits_command."
    exit 2
endif
if ( -r /home/dev/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command ) then
    alias send_command "/home/dev/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command"
else if ( -r /icc/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command ) then
    alias send_command "/icc/bin/autoguider/commandserver/test/${HOSTTYPE}/send_command"
else
    echo "Could not find test_getfits_command."
    exit 2
endif
if ( -r /home/dev/bin/autoguider/scripts/print_line.awk ) then
    alias print_line "/home/dev/bin/autoguider/scripts/print_line.awk"
else if ( -r /icc/bin/autoguider/scripts/print_line.awk ) then
    alias print_line "/icc/bin/autoguider/scripts/print_line.awk"
else
    echo "Could not find print_line.awk."
    exit 3
endif
set hostname = "acc"
set port = "6571"
set filename = ""
set last_filename = ""
set done = 0
while ( "${done}" == "0" )
    #
    # do expose
    #
    set expose_output = `send_command -h ${hostname} -p ${port} -c "expose ${exposure_length}"`
    echo "expose ${exposure_length} returned : ${expose_output}"
    set expose_output = `echo "${expose_output}" | sed "s/.*client: reply:\(.*\)/\1/"`
    echo "expose output now ${field_output}."
    set retval = `echo "${expose_output}" | /usr/bin/awk ' { print $1}'`
    echo "retval is ${retval}."
    if( "${retval}" != "0" ) then
	    echo "expose failed (${retval})."
	    exit 1
    endif
    #
    # get output fits
    #
    set file_number = 0
    set date_string = `/bin/date +"%Y-%m-%dT%H:%M:%S"`
    set last_filename = "${filename}"
    set filename = "${filename_dir}/expose_${frame_type_string}_${date_string}_${file_number}.fits"
    getfits_command -h ${hostname} -p ${port} -c "getfits field ${frame_type_string}" -f ${filename}
    if( "${ds9disp}" == "1" ) then
	set ds9active = `xpaaccess ds9`
	if( "${ds9active}" == "yes" ) then
	    xpaset -p ds9 file ${filename}
	else
	    ds9 ${filename}&
	endif
    endif
    #
    # delete the last filename before this one, if configured to do so, and a last_filename exists
    #
    if( ${delete_frames} ) then
       if( "${last_filename}" != "" ) then
	   rm ${last_filename}
       endif
    endif
end
