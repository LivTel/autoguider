#!/bin/csh
# $Header: /home/cjm/cvs/autoguider/scripts/autoguider_guide,v 1.2 2013-12-09 10:25:41 cjm Exp $
set ds9disp = 0
set exposure_length = 100
set frame_type_string = "reduced"
set object_index = -1
if ( $#argv > 0 ) then
    set next_arg = ""
    foreach arg ( $argv )
	if( "$arg" == "-ds9disp" ) then
		set ds9disp = 1
	else if( "$arg" == "-exposure_length" ) then
		set next_arg = "exposure_length"
	else if( "$arg" == "-help" ) then
		echo "autoguider_guide [-exposure_length <ms>][-object <index>][-help][-ds9disp]"
		exit 1
	else if( "$arg" == "-object" ) then
		set next_arg = "object"
	else 
		if( "${next_arg}" == "exposure_length" ) then
		    set exposure_length = "${arg}"
		    set next_arg = ""
		else if( "${next_arg}" == "object" ) then
		    set object_index = "${arg}"
		    set next_arg = ""
		else
			echo "Unknown argument: $arg"
			exit 1
		endif
        endif
    end
endif

if ( -r /home/dev/bin/autoguider/commandserver/test/i386-linux/test_getfits_command ) then
    alias getfits_command "/home/dev/bin/autoguider/commandserver/test/i386-linux/test_getfits_command"
else if ( -r /icc/bin/autoguider/commandserver/test/i386-linux/test_getfits_command ) then
    alias getfits_command "/icc/bin/autoguider/commandserver/test/i386-linux/test_getfits_command"
else
    echo "Could not find test_getfits_command."
    exit 2
endif
if ( -r /home/dev/bin/autoguider/commandserver/test/i386-linux/send_command ) then
    alias send_command "/home/dev/bin/autoguider/commandserver/test/i386-linux/send_command"
else if ( -r /icc/bin/autoguider/commandserver/test/i386-linux/send_command ) then
    alias send_command "/icc/bin/autoguider/commandserver/test/i386-linux/send_command"
else
    echo "Could not find test_getfits_command."
    exit 2
endif
if ( -r /home/dev/bin/autoguider/scripts/print_line.awk ) then
    alias print_line "/home/dev/bin/autoguider/scripts/print_line.awk"
else if ( -r /icc/bin/autoguider/scripts/print_line.awk ) then
    alias print_line "/icc/bin/autoguider/scripts/print_line.awk"
else
    echo "Could not find print_line.awk."
    exit 3
endif
set hostname = "acc"
set port = "6571"
#
# do guide exposure_length
#
set guide_output = `send_command -h ${hostname} -p ${port} -c "guide exposure_length ${exposure_length}"`
echo "guide exposure_length ${exposure_length} returned : ${guide_output}"
set retval = `echo "${guide_output}" | /usr/bin/awk ' { print $1}'`
echo "retval is ${retval}."
if( "${retval}" != "0" ) then
	echo "guide exposure_length ${exposure_length} failed (${retval})."
	exit 1
endif
#
# object selection
#
set guide_output = `send_command -h ${hostname} -p ${port} -c "guide object ${object_index}"`
echo "guide object ${object_index} returned : ${guide_output}"
set retval = `echo "${guide_output}" | /usr/bin/awk ' { print $1}'`
echo "retval is ${retval}."
if( "${retval}" != "0" ) then
	echo "guide object ${object_index} failed (${retval})."
	exit 1
endif
#
# turn guide loop on
#
set guide_output = `send_command -h ${hostname} -p ${port} -c "guide on"`
echo "guide on returned : ${guide_output}"
set retval = `echo "${guide_output}" | /usr/bin/awk ' { print $1}'`
echo "retval is ${retval}."
if( "${retval}" != "0" ) then
	echo "guide on failed (${retval})."
	exit 1
endif
#
# loop displaying guide frames
#
set directory = `dirname $0`
${directory}/autoguider_get_guide_frames -ds9disp -ds9objects -reduced -delete
#
# $Log: not supported by cvs2svn $
# Revision 1.1  2006/05/25 16:24:10  cjm
# Initial revision
#
#
